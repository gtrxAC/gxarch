addr MOUSELTIME 0xFF05   ; how many frames has left/right mouse been down
addr MOUSERTIME 0xFF06
addr MOUSELPRESS 0xFF07  ; has left/right mouse been released this frame
addr MOUSERPRESS 0xFF08

; ______________________________________________________________________________
;
;  Update mouse subroutine
;
;  Notes:
;  - This subroutine doesn't use any registers other than immediates %0 %1.
;  - This subroutine should be called every frame to update
;    MOUSE L/R TIME/PRESS variables.
;  - You can test this subroutine with the mouse example.
; ______________________________________________________________________________
;
updatemouse:
	st (%0:0) MOUSELPRESS
	st %0 MOUSERPRESS

	; If left mouse down, increase its down time
	cj [%0:MOUSEL] updmouse_ldown

	; If left mouse was released, set MOUSELPRESS
	cjs [%0:MOUSELTIME] updmouse_lrelease

	; If not down, reset down time
	st (%0:0) MOUSELTIME
	jmp updmouse_lnotdown

updmouse_ldown:
	ld %1 MOUSELTIME
	add %0 %1 %1      ; note: %0 already contains 1 because it was loaded with
	                  ; MOUSEL which should be 1
	st %1 MOUSELTIME

updmouse_lnotdown:
	; If right mouse down, increase its down time
	cj [%0:MOUSER] updmouse_rdown

	; If right mouse was released, set MOUSERPRESS
	cjs [%0:MOUSERTIME] updmouse_rrelease

	; If not down, reset down time
	st (%0:0) MOUSERTIME
	jmp updmouse_rnotdown

updmouse_rdown:
	ld %1 MOUSERTIME
	add %0 %1 %1
	st %1 MOUSERTIME

updmouse_rnotdown:
	ret

; ______________________________________________________________________________
;
;  Set mouse pressed variables (subroutines)
; ______________________________________________________________________________
;
updmouse_lrelease:
	st (%0:1) MOUSELPRESS
	ret

updmouse_rrelease:
	st (%0:1) MOUSERPRESS
	ret