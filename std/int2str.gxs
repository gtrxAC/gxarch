; ______________________________________________________________________________
;
;  Integer to string conversion subroutine
;
;  Arguments:
;    %20 = integer to convert (unsigned 8-bit)
;    %21 %22 = destination string pointer
;
;  Uses registers:
;    %23 = division result
;
;  Notes:
;  - A null terminator is not added to the end of the string.
;  - 3 digits are always used, so 1 is converted into "001".
;  - If the position you want to write to is not the beginning of the string
;    pointer, you'll have to calculate the address yourself (see mouse example).
; ______________________________________________________________________________
;
.include std/common.gxs

int2str: {
	reg src arg0    ; source integer to convert
	reg desth arg1  ; destination string address, high byte
	reg destl arg2  ; destination string address, low byte
	reg divres %23  ; division result

	div src (%0:100) divres      ; divide by 100 to get 3rd digit
	add divres (%0:0x30) divres  ; number + 0x30 = ASCII digit of number
	sti divres desth             ; store digit into string

	add destl (%0:1) destl  ; next character
	add desth %h desth

	div %r (%0:10) divres        ; divide remainder by 10 to get 2nd digit
	add divres (%0:0x30) divres  ; ...
	sti divres desth

	add destl (%0:1) destl
	add desth %h desth

	add %r (%0:0x30) divres
	sti divres desth
	ret
}