; ______________________________________________________________________________
;
;  Button
;  Draws a button with a different image depending on if it's hovered or
;  pressed, and returns true if it's pressed.
;
;  Arguments:
;    %20 = X location
;    %21 = Y location
;    %22 = image source X
;    %23 = image source Y
;    %24 = image source width
;    %25 = image source height
;    %26 = return value (has the button been pressed)
;
;  Notes:
;  - The button images should be stacked on top of each other in the tileset,
;    the hovered button under the normal button and the pressed button under the
;    hovered button.
; ______________________________________________________________________________
;
.include std/common.gxs

button: {
	reg x arg0
	reg y arg1
	reg sx arg2
	reg sy arg3
	reg w arg4
	reg h arg5
	reg result arg6

	set result 0

	ld %0 MOUSEX
	.ltj %0 x button_draw

	add x w %1
	.gtj %0 %1 button_draw

	ld %0 MOUSEY
	.ltj %0 y button_draw

	add y h %1
	.gtj %0 %1 button_draw

	ld %0 MOUSEL
	cj %0 button_down

	ld %0 MOUSELPRESS
	cj %0 button_press

	add sy h sy
	jmp button_draw

button_press:
	set result 1

button_down:
	add sy h sy
	add sy h sy

button_draw:
	dw sx sy w h
	at x y
	ret
}