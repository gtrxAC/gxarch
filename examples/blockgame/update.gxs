update:
	; Only allow moving every SPEED frames
	ld %0 framecount
	div %0 (%1:SPEED) %29
	cj %r getmouseblock

	reg WW %5
	reg BCOUNT %7
	reg WH %8
	reg _13 %9

	set %1 0
	set %2 1
	set WW WWIDTH
	set BCOUNT BLOCKCOUNT
	set WH WHEIGHT

	; Screen can show 13 blocks in both X/Y at a time, so maximum possible
	; player X/Y is (world width/height - 13) so we don't try to draw out of
	; bounds (top left block on screen is player position)
	sub WW (_13:13) WW
	sub WH _13 WH

	keyjs K_w moveup
	keyjs K_a moveleft
	keyjs K_s movedown
	keyjs K_d moveright
; ______________________________________________________________________________
;
;  Previous/next block keys
; ______________________________________________________________________________
;
	; If Z is pressed, select previous block, else reset down timer
	keyj K_z prevblock
	jmp noprevblock
prevblock:
	ld %0 zdowntime
	cj %0 checknextblock

	ld %4 curblock
	eqj %4 %2 checknextblock
	sub %4 %2 %4
	st %4 curblock

	; Increment counter
	ld %0 zdowntime
	add %0 %2 %0
	st %0 zdowntime
	jmp checknextblock
noprevblock:
	st %1 zdowntime

checknextblock:
	; If X is pressed, select next block, else reset down timer
	keyj K_x nextblock
	jmp nonextblock
nextblock:
	ld %0 xdowntime
	cj %0 getmouseblock

	ld %4 curblock
	add %4 %2 %4
	eqj %4 BCOUNT getmouseblock
	st %4 curblock

	; Increment counter
	ld %0 xdowntime
	add %0 %2 %0
	st %0 xdowntime
	jmp getmouseblock
nonextblock:
	st %1 xdowntime
; ______________________________________________________________________________
;
;  Mouse
; ______________________________________________________________________________
;
; Get the block that the mouse is pointing at
getmouseblock:
	ld %0 MOUSEX
	div %0 (%2:BLOCKSIZE) %0
	ld %1 playerx
	add %0 %1 %0
	st %0 mousexblock

	ld %0 MOUSEY
	div %0 %2 %0
	ld %1 playery
	add %0 %1 %0
	st %0 mouseyblock

	; If left mouse is down, replace pointed block with air
	ld %0 MOUSEL
	cjs %0 removeblock

	; If right mouse is down, replace pointed block with current block
	ld %0 MOUSER
	cjs %0 placeblock
	jmp render
; ______________________________________________________________________________
;
;  Subroutines
; ______________________________________________________________________________
;
; Get address of block being pointed at
getblockaddr:
	; block address = starting address
	set %0 hi(blocks)
	set %1 lo(blocks)

	; block address += mouse y block * world width
	ld %2 mouseyblock
	mul %2 (%3:WWIDTH) %2
	add %0 %h %0
	add %1 %2 %1

	; block address += mouse x block
	ld %2 mousexblock
	add %1 %2 %1
	add %0 %h %0
	ret

moveup:
	ld %4 playery
	eqj %4 %1 moveuplimit
	sub %4 %2 %4
	st %4 playery
moveuplimit:
	ret

moveleft:
	ld %4 playerx
	eqj %4 %1 moveleftlimit
	sub %4 %2 %4
	st %4 playerx
moveleftlimit:
	ret

movedown:
	ld %4 playery
	add %4 %2 %4
	gtj %4 WH movedownlimit
	st %4 playery
movedownlimit:
	ret

moveright:
	ld %4 playerx
	add %4 %2 %4
	gtj %4 WW moverightlimit
	st %4 playerx
moverightlimit:
	ret

removeblock:
	js getblockaddr
	sti (%4:0) %0
	ret

placeblock:
	js getblockaddr
	ld %4 curblock
	sti %4 %0
	ret