; Key for gxarch
; Saves the last pressed key and shows it on screen
; Date: 2021-11-21
; Author: gtrxAC

dat main

string: dat "Last pressed key: 000 \" \""

val FORMATNUM 20    ; addresses of where numeric/ASCII values are
val FORMATASCII 25  ; formatted into the string

val PRINT_CHARSPERLINE 16
val PRINT_WIDTH 4
val PRINT_HEIGHT 8
.include std/common.gxs
.include std/print.gxs
.include std/int2str.gxs

main:
	set %0 1          ; enable SRAM
	st %0 SRAM_TOGGLE

loop:
	key %0 %1       ; is current key pressed?
	cj %1 keyfound  ; then store it in SRAM

	set %1 1             ; next key
	add %0 %1 %0
	set %1 lo(K_COUNT)
	.ltj %0 %1 loop

loopend:
	ld %20 SRAM           ; update string with last key's numeric value
	set %21 hi(FORMATNUM)
	set %22 lo(FORMATNUM)
	js int2str

	st %20 FORMATASCII  ; update string with last key's ASCII value

	set %20 hi(string)  ; print string on screen
	set %21 lo(string)
	set %22 0
	set %23 0
	js print

	end       ; draw frame and loop
	set %0 0
	jmp loop

keyfound:
	st %0 SRAM
	jmp loopend