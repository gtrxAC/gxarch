; ______________________________________________________________________________
;
;  Hex character to integer conversion subroutine
;
;  Arguments:
;    %20 = character to convert (0-9 a-f A-F)
;    %21 = return value (value from 0x00 to 0x0F)
;
;  Notes:
;  - 16 (0x10) is returned if the character is not 0-9 a-f A-F
; ______________________________________________________________________________
;
hex2int4b:
	; If '/' < input < ':' it's an ASCII digit
	gt arg0 (%0:K_SLASH) %1
	lt arg0 (%0:K_COLON) %2
	and %1 %2 %1
	cj %1 hex2int4b_digit

	; If '@' < input < 'G' it's uppercase A-F
	gt arg0 (%0:K_AT) %1
	lt arg0 (%0:K_G) %2
	and %1 %2 %1
	cj %1 hex2int4b_upper

	; If '`' < input < 'g' it's lowercase a-f
	gt arg0 (%0:K_GRAVE) %1
	lt arg0 (%0:K_g) %2
	and %1 %2 %1
	cj %1 hex2int4b_lower

	set arg1 16
	ret

hex2int4b_digit:
	; Subtract 0x30 to get the ASCII digit's numeric value
	sub arg0 (%0:0x30) arg1
	ret

hex2int4b_upper:
	; 'A' = 0x41
	; 0x41 - 0x37 = 0x0A
	sub arg0 (%0:0x37) arg1
	ret

hex2int4b_lower:
	; 'a' = 0x61
	; 0x61 - 0x57 = 0x0A
	sub arg0 (%0:0x57) arg1
	ret