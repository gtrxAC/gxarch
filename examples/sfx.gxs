; SFX for gxarch
; Sound effect generator
; Date: 2022-02-21
; Author: gtrxAC

datl main          ; set program entry point
dat 160, 160, 176  ; set screen clear color (gray)
dat 0              ; doesn't support save file

titlestr: dat "gxarch: sound effects"
typestr: dat "Type:"
freqstr: dat "Frequency:"
sustainstr: dat "Sustain:"
decaystr: dat "Decay:"

int2strbuf: dat "000"

val PRINT_CHARSPERLINE 32
val PRINT_WIDTH 4
val PRINT_HEIGHT 8
include "std/common.gxs"
include "std/print.gxs"
include "std/int2str.gxs"
include "std/mouse.gxs"
include "std/button.gxs"

val BUTTONY 32
val BUTTONW 10
val BUTTONH 10

val SQUAREBTNX 0
val SAWTOOTHBTNX 10
val SINEBTNX 20
val NOISEBTNX 30
val LEFTBTNX 40
val RIGHTBTNX 50
val PLAYBTNX 60
val PLAYBTNW 20

main:
	reg SND0 %4
	reg SND1 %5
	reg SND2 %6
	reg SND3 %7

	set SND1 100
	set SND2 50
	set SND3 50

loop:
	js updatemouse

	set arg0 hi(titlestr)
	set arg1 lo(titlestr)
	set arg2 22
	set arg3 8
	js print

; ______________________________________________________________________________
;
;  Wave type
; ______________________________________________________________________________
;
	set arg0 hi(typestr)
	set arg1 lo(typestr)
	set arg2 40
	set arg3 32
	js print

	set arg0 64
	set arg1 31
	set arg2 SQUAREBTNX
	js btncommon
	cjs btndown settype0

	set arg0 75
	set arg1 31
	set arg2 SAWTOOTHBTNX
	js btncommon
	cjs btndown settype1

	set arg0 86
	set arg1 31
	set arg2 SINEBTNX
	js btncommon
	cjs btndown settype2

	set arg0 97
	set arg1 31
	set arg2 NOISEBTNX
	js btncommon
	cjs btndown settype3

; ______________________________________________________________________________
;
;  Frequency
; ______________________________________________________________________________
;
	set arg0 hi(freqstr)
	set arg1 lo(freqstr)
	set arg2 20
	set arg3 48
	js print

	set arg0 64
	set arg1 47
	set arg2 LEFTBTNX
	js btncommon
	cjs btndown decfreq

	mov SND1 arg0
	set arg1 hi(int2strbuf)
	set arg2 lo(int2strbuf)
	js int2str

	set arg0 hi(int2strbuf)
	set arg1 lo(int2strbuf)
	set arg2 78
	set arg3 48
	js print

	set arg0 94
	set arg1 47
	set arg2 RIGHTBTNX
	js btncommon
	cjs btndown incfreq

; ______________________________________________________________________________
;
;  Sustain
; ______________________________________________________________________________
;
	set arg0 hi(sustainstr)
	set arg1 lo(sustainstr)
	set arg2 28
	set arg3 64
	js print

	set arg0 64
	set arg1 63
	set arg2 LEFTBTNX
	js btncommon
	cjs btndown decsust

	mov SND2 arg0
	set arg1 hi(int2strbuf)
	set arg2 lo(int2strbuf)
	js int2str

	set arg0 hi(int2strbuf)
	set arg1 lo(int2strbuf)
	set arg2 78
	set arg3 64
	js print

	set arg0 94
	set arg1 63
	set arg2 RIGHTBTNX
	js btncommon
	cjs btndown incsust

; ______________________________________________________________________________
;
;  Decay
; ______________________________________________________________________________
;
	set arg0 hi(decaystr)
	set arg1 lo(decaystr)
	set arg2 36
	set arg3 80
	js print

	set arg0 64
	set arg1 79
	set arg2 LEFTBTNX
	js btncommon
	cjs btndown decdecay

	mov SND3 arg0
	set arg1 hi(int2strbuf)
	set arg2 lo(int2strbuf)
	js int2str

	set arg0 hi(int2strbuf)
	set arg1 lo(int2strbuf)
	set arg2 78
	set arg3 80
	js print

	set arg0 94
	set arg1 79
	set arg2 RIGHTBTNX
	js btncommon
	cjs btndown incdecay

; ______________________________________________________________________________
;
;  Play button
; ______________________________________________________________________________
;
	set arg0 54
	set arg1 104
	set arg2 PLAYBTNX
	set arg3 BUTTONY
	set arg4 PLAYBTNW
	set arg5 BUTTONH
	js button
	cjs btndown playsound

	end
	jmp loop

; ______________________________________________________________________________
;
;  Button press actions and other subroutines
; ______________________________________________________________________________
;

; Shorthand for drawing a button with the same source Y, width and height.
btncommon:
	set arg3 BUTTONY
	set arg4 BUTTONW
	set arg5 BUTTONH
	js button
	ret

; ______________________________________________________________________________
;
;  Wave type subroutines
;  Called when top row buttons are pressed
; ______________________________________________________________________________
;
settype0:
	set SND0 0
	jmp playsound

settype1:
	set SND0 1
	jmp playsound

settype2:
	set SND0 2
	jmp playsound

settype3:
	set SND0 3
	jmp playsound

; ______________________________________________________________________________
;
;  Frequency subroutines
; ______________________________________________________________________________
;
decfreq:
	sub SND1 (%0:10) SND1
	cjs %h setfreq0
	jmp playsound

setfreq0:
	set SND1 0
	ret

incfreq:
	add SND1 (%0:10) SND1
	cjs %h setfreqmax
	jmp playsound

setfreqmax:
	set SND1 250
	ret

; ______________________________________________________________________________
;
;  Sustain subroutines
; ______________________________________________________________________________
;
decsust:
	sub SND2 (%0:10) SND2
	cjs %h setsust0
	jmp playsound

setsust0:
	set SND2 0
	ret

incsust:
	add SND2 (%0:10) SND2
	cjs %h setsustmax
	jmp playsound

setsustmax:
	set SND2 250
	ret

; ______________________________________________________________________________
;
;  Decay subroutines
; ______________________________________________________________________________
;
decdecay:
	sub SND3 (%0:10) SND3
	cjs %h setdecay0
	jmp playsound

setdecay0:
	set SND3 0
	ret

incdecay:
	add SND3 (%0:10) SND3
	cjs %h setdecaymax
	jmp playsound

setdecaymax:
	set SND3 250
	ret

; ______________________________________________________________________________
;
;  Play sound
; ______________________________________________________________________________
;
playsound:
	snd SND0 SND1 SND2 SND3
	ret