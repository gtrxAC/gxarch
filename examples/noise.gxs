; Noise for gxarch
; Draws white noise on the screen, random black or white for each pixel.
; Date: 2021-12-31
; Author: gtrxAC

datl main    ; set program entry point
dat 0, 0, 0  ; set screen clear color (black)
dat 0        ; doesn't support save file

include "std/common.gxs"

main:
	; This example doesn't operate on main memory, it only uses registers:
	reg INC %0   ; increment and draw size (constant 1)
	reg SIZE %1  ; screen size and random value division (constant 128)
	reg SY %2    ; draw source Y (constant 0), initial value is 0 so we don't
	             ; need to set it
	reg sx %3    ; draw source X (random value 0 - 1)
	reg x %4     ; X/Y iterator
	reg y %5

	; Set constants
	set INC 1
	set SIZE 128
loop:
	set y 0
loopy:
	set x 0
loopx:
	; Note: the tileset is only two pixels, (0, 0) is black and (1, 0) is white

	ld sx RAND         ; generate random value 0-1 for %3
	div sx SIZE sx     ; RAND has 0 - 255, divide by 128 to get 0 - 1
	dw sx SY INC INC   ; draw the pixel from the tileset
	at x y             ; at the coordinates of the current iterator

	; Next X iteration
	add x INC x
	ltj x SIZE loopx

	; Next Y iteration
	add y INC y
	ltj y SIZE loopy

	; Draw frame and end loop
	end
	jmp loop